# Maintainer: Daniel Dada <dan@binarygoose.dev>
pkgname=clrsync
pkgver=0.1.5
pkgrel=1
pkgdesc="Color scheme manager"
arch=('x86_64')
url="https://github.com/obsqrbtz/clrsync"
license=('MIT')

depends=(
    glfw
    freetype2
    fontconfig
    mesa
    libglvnd
    libxcursor
    gtk3
)

makedepends=(
  cmake
  glfw
  libx11
  libxrandr
  libxi
  libxinerama
  libxcursor
  wayland
  wayland-protocols
  gtk3
)

source=("$pkgname-$pkgver.tar.gz::https://github.com/obsqrbtz/clrsync/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('SKIP')

build() {
    cd "$pkgname-$pkgver"
    cmake -B build -S . \
        -DCMAKE_BUILD_TYPE=Release \
        -DUSE_SYSTEM_GLFW=ON \
        -DCMAKE_INSTALL_PREFIX=/usr
    cmake --build build
}

package() {
    cd "$pkgname-$pkgver"
    DESTDIR="$pkgdir" cmake --install build
    install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
